namespace MyDataBot.Bot.Application.Commands

open System
open MediatR
open Microsoft.Extensions.Logging
open MyDataBot.Bot.Core.Common.CommonBotModels
open MyDataBot.Bot.Core.Common.TelegramBotCoreModels
open MyDataBot.Bot.Core.Common.Message
open MyDataBot.Bot.Core.UseCases.AskQuestion
open MyDataBot.Bot.Core.UseCases.AskQuestion.MessagePreProcessor
open MyDataBot.Bot.Dependencies.AsyncBotMessageProcessing
open MyDataBot.Bot.Dependencies.Data
open MyDataBot.Bot.Core.Common.BotDataFunctions


[<RequireQualifiedAccess>]
type SendNewBotMessageForProcessingCommandResponse =
    | Accepted
    | ShortcutProcessed
    | Forbidden

type SendNewBotMessageForProcessingCommand = Command<IncomingBotMessage, SendNewBotMessageForProcessingCommandResponse>

type ProcessNewBotMessageCommandHandler
    (
        mediator: IMediator,
        logger: ILogger<ProcessNewBotMessageCommandHandler>,
        messageLog: IMessageLog,
        quotaRepo: IQuotaRepository,
        queue: IProcessMessageQueue
    ) =
    interface IRequestHandler<SendNewBotMessageForProcessingCommand, SendNewBotMessageForProcessingCommandResponse> with
        member this.Handle(request, cancellationToken) =
            let processShortcut messageText resultType =
                let createSendMessageCommand text : SendMessageCommand =
                    match request.Data.SpecificMetadata with
                    | BotMessageMetadata.TelegramMessageMetadata tgMetadata ->
                        Command.createFrom
                            request
                            { Text = text
                              SendTo =
                                SendMessageTo.Telegram
                                    { BotId = tgMetadata.BotId
                                      ChatId = tgMetadata.ChatId } }

                task {
                    do! messageLog.SaveMessage cancellationToken false request.Data
                    let sendMsgCommand = createSendMessageCommand messageText
                    let! sendMsgResult = mediator.Send(sendMsgCommand)

                    match sendMsgResult with
                    | Ok _ ->
                        do!
                            task {
                                let response =
                                    { Id = BotMessageResponseId(Ulid.NewUlid())
                                      Delivered = true
                                      Text = messageText
                                      DateTimeUtc = DateTime.UtcNow
                                      IncomingMessageId = request.Data.Id
                                      ResponseType = resultType }
                                    : BotMessageResponse

                                do! messageLog.SaveResponse cancellationToken response
                            }

                        return SendNewBotMessageForProcessingCommandResponse.ShortcutProcessed
                    | Error e ->
                        logger.LogError("Shortcut process {Error} for command {SendMessageCommand}", e, sendMsgCommand)
                        return SendNewBotMessageForProcessingCommandResponse.Accepted
                }

            task {
                match MessagePreProcessor.getType request.Data with
                | MessageType.Command cmd ->
                    match cmd with
                    | MessageCommand.Start -> return! processShortcut "Ð¿Ñ€Ð¸Ð²Ñ–Ñ‚!" ResponseType.NotAiOk
                | MessageType.Invalid(str, msg) ->
                    match str with
                    | InvalidMessageType.Empty ->
                        return!
                            processShortcut
                                "Ð½Ðµ Ð¼Ð¾Ð¶Ñƒ Ð¾Ð¿Ñ€Ð°Ñ†ÑŽÐ²Ð°Ñ‚Ð¸ Ð¿Ð¾Ð²Ñ–Ð´Ð¾Ð¼Ð»ÐµÐ½Ð½Ñ Ð±ÐµÐ· Ñ‚ÐµÐºÑÑ‚Ñƒ ðŸ˜”"
                                ResponseType.NotAiGenericError
                | MessageType.AiQuestion msg ->
                    let botId = IncomingBotMessage.getBotId msg
                    let! quotaOption = quotaRepo.TryGetQuotaByBotId cancellationToken botId

                    match quotaOption with
                    | None ->
                        logger.LogWarning("Quota not found for {BotId}", botId)

                        return!
                            processShortcut
                                "Ñ‚Ð°Ñ€Ð¸Ñ„Ð½Ð¸Ð¹ Ð¿Ð»Ð°Ð½ Ð½Ðµ Ð·Ð½Ð°Ð¹Ð´ÐµÐ½Ð¸Ð¹, Ð¾Ð¿Ð»Ð°Ñ‚Ñ–Ñ‚ÑŒ Ð¿Ð¾ÑÐ»ÑƒÐ³Ð¸ Ð°Ð±Ð¾ Ð·Ð²Ê¼ÑÐ¶Ñ–Ñ‚ÑŒÑÑ Ð· ÑÐ»ÑƒÐ¶Ð±Ð¾ÑŽ Ð¿Ñ–Ð´Ñ‚Ñ€Ð¸Ð¼ÐºÐ¸"
                                ResponseType.NotAiQuotaError
                    | Some quota ->
                        let decision = AskQuestion.ask request.DateTimeUtc quota msg

                        match decision with
                        | Error e ->
                            let! _ =
                                match e with
                                | AskQuestionError.NoData ->
                                    processShortcut
                                        "Ð½Ðµ Ð·Ð½Ð°Ð¹Ð´ÐµÐ½Ð¾ Ð´Ð°Ð½Ð¸Ñ…, Ð±ÑƒÐ´ÑŒ Ð»Ð°ÑÐºÐ°, Ð·Ð°Ð²Ð°Ð½Ñ‚Ð°Ð¶Ñ‚Ðµ Ð´Ð¾ÐºÑƒÐ¼ÐµÐ½Ñ‚"
                                        ResponseType.NotAiQuotaError
                                | AskQuestionError.AccessRestricted ->
                                    processShortcut
                                        "Ð´Ð¾ÑÑ‚ÑƒÐ¿ Ð´Ð¾ Ñ€ÐµÑÑƒÑ€ÑÑƒ Ð¾Ð±Ð¼ÐµÐ¶ÐµÐ½Ð¸Ð¹, Ð·Ð²ÐµÑ€Ð½Ñ–Ñ‚ÑŒÑÑ Ð´Ð¾ Ð°Ð´Ð¼Ñ–Ð½Ñ–ÑÑ€Ð°Ñ‚Ð¾Ñ€Ð°"
                                        ResponseType.NotAiAccessError
                                | AskQuestionError.QuotaOutdated _ ->
                                    processShortcut
                                        "Ñ‡Ð°Ñ Ð²Ð¸ÐºÐ¾Ñ€Ð¸ÑÑ‚Ð°Ð½Ð½Ñ Ð·Ð±Ñ–Ð³, Ð±ÑƒÐ´ÑŒ Ð»Ð°ÑÐºÐ° Ð¾Ð¿Ð»Ð°Ñ‚Ñ–Ñ‚ÑŒ Ð¿Ð¾ÑÐ»ÑƒÐ³Ð¸, Ð°Ð±Ð¾ Ð·Ð²ÐµÑ€Ð½Ñ–Ñ‚ÑŒÑÑ Ð´Ð¾ ÑÐ»ÑƒÐ¶Ð±Ð¸ Ð¿Ñ–Ð´Ñ‚Ñ€Ð¸Ð¼ÐºÐ¸"
                                        ResponseType.NotAiQuotaError
                                | AskQuestionError.MessageLimitReached _ ->
                                    processShortcut
                                        "Ð´Ð¾ÑÑÐ³Ð½ÑƒÑ‚Ð¾ Ð»Ñ–Ð¼Ñ–Ñ‚Ñƒ Ð¿Ð¾Ð²Ñ–Ð´Ð¾Ð¼Ð»ÐµÐ½ÑŒ, Ð±ÑƒÐ´ÑŒ Ð»Ð°ÑÐºÐ° Ð¾Ð¿Ð»Ð°Ñ‚Ñ–Ñ‚ÑŒ Ð¿Ð¾ÑÐ»ÑƒÐ³Ð¸, Ð°Ð±Ð¾ Ð·Ð²ÐµÑ€Ð½Ñ–Ñ‚ÑŒÑÑ Ð´Ð¾ ÑÐ»ÑƒÐ¶Ð±Ð¸ Ð¿Ñ–Ð´Ñ‚Ñ€Ð¸Ð¼ÐºÐ¸"
                                        ResponseType.NotAiQuotaError

                            logger.LogInformation("Forbidden to ask with {Error}", e)
                            return SendNewBotMessageForProcessingCommandResponse.Forbidden
                        | Ok msg ->
                            do!
                                task {
                                    do! request.Data |> messageLog.SaveMessage cancellationToken true

                                    let setChatActionCommand: SetChatActionCommand =
                                        { Bot =
                                            match request.Data.SpecificMetadata with
                                            | BotMessageMetadata.TelegramMessageMetadata tg ->
                                                SetChatActionBot.Telegram { BotId = tg.BotId; ChatId = tg.ChatId }
                                          Action = ChatAction.Typing }
                                        |> Command.createFrom request

                                    let! _ = mediator.Send(setChatActionCommand, cancellationToken)

                                    let message =
                                        { MessageId = msg.Id
                                          MessageText = msg.Text
                                          DataFolder = Option.get quota.DataFolder }

                                    do! queue.PushToQueue request.CorrelationId message
                                }

                            return SendNewBotMessageForProcessingCommandResponse.Accepted
            }
